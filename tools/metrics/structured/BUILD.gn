import("//mojo/public/tools/bindings/mojom.gni")

action("gen_structured_events") {
  script = "//tools/metrics/structured/gen_events.py"

  # Re-generate the outputs if the codegen code changes:
  inputs = [
    "//tools/metrics/structured/codegen.py",
    "//tools/metrics/structured/events_template.py",
    "//tools/metrics/structured/gen_events.py",
  ]
  sources = [ "//tools/metrics/structured/structured.xml" ]

  outdir = "$target_gen_dir"

  outputs = [
    outdir + "/structured_events.cc",
    outdir + "/structured_events.h",
  ]

  args = [
    "--input",
    rebase_path(sources[0], root_build_dir),
    "--output",
    rebase_path(outdir, root_build_dir),
  ]
}

static_library("structured_events") {
  sources = get_target_outputs(":gen_structured_events")

  deps = [
    ":gen_structured_events",
    "//base",
  ]
}
